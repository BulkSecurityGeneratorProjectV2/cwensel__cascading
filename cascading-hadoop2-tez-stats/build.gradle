/*
 * Copyright (c) 2016-2019 Chris K Wensel <chris@wensel.net>. All Rights Reserved.
 * Copyright (c) 2007-2017 Xplenty, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext.hadoop2Version = '2.10.0'
ext.tezVersion = '0.8.2'

if( System.properties[ 'hadoop2tez.release.final' ] )
  tezVersion = System.properties[ 'hadoop2tez.release.final' ]

repositories {

  if( tezVersion.contains( 'SNAPSHOT' ) )
    maven {url = 'https://repository.apache.org/content/repositories/snapshots'}
}

dependencies {

  api project( ':cascading-core' )
  api project( ':cascading-hadoop2-io' )

  // forces update of yarn slf4j dependencies
  compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
  compileOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.5'

  compileOnly( group: 'org.apache.tez', name: 'tez-api', version: tezVersion )
  compileOnly( group: 'org.apache.tez', name: 'tez-dag', version: tezVersion )
  compileOnly( group: 'org.apache.tez', name: 'tez-mapreduce', version: tezVersion )

  if( !hadoop2Version.startsWith( '2.4' ) ) // !hadoop 2.4
    {
    compileOnly( group: 'org.apache.hadoop', name: 'hadoop-aws', version: hadoop2Version )
    }
}

configurations.all {
  resolutionStrategy {

    def versions = ['0.6.0', '0.6.1', '0.6.2', '0.7.0', '0.8.2']

    if( !hadoop2Version.startsWith( '2.4' ) && versions.contains( tezVersion ) )
      force 'com.google.guava:guava:11.0.2'

    eachDependency {def details ->
      if( details.requested.group == 'org.apache.hadoop' )
        details.useVersion hadoop2Version
    }
  }
}

javadoc {

  classpath = files( project.sourceSets.test.compileClasspath )

  configure( options ) {
    links << "http://hadoop.apache.org/docs/r${hadoop2Version}/api/".toString()
    links << "http://tez.apache.org/releases/${tezVersion}/tez-api-javadocs/".toString()
  }
}
