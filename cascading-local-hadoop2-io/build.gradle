/*
 * Copyright (c) 2016-2019 Chris K Wensel <chris@wensel.net>. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: '../etc/testingPlatform.gradle'

ext.hadoop2Version = '2.7.3'

if( System.properties[ 'hadoop2mr1.release.final' ] )
  hadoop2Version = System.properties[ 'hadoop2mr1.release.final' ]

dependencies {
  api project( ':cascading-local' )
  api project( ':cascading-hadoop2-io' )

  compileOnly( group: 'org.apache.hadoop', name: 'hadoop-mapreduce-client-core', version: hadoop2Version )
  compileOnly( group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoop2Version )

  testImplementation project( path: ':cascading-platform', configuration: 'testArtifacts' )
  testImplementation project( path: ':cascading-hadoop2-mr1', configuration: 'testArtifacts' )
}

configurations.all {
  resolutionStrategy {

    if( !hadoop2Version.startsWith( '2.4' ) )
      force 'com.google.guava:guava:11.0.2'
  }
}

test {

  delete( "target" ) // hardcoded path in hadoop2

  forkEvery = 1 // static fields on the platform test get munged otherwise

  systemProperties[ 'hadoop.log.dir' ] = new String( "${buildDir}/test/log" )
  systemProperties[ 'hadoop.tmp.dir' ] = new String( "${buildDir}/test/tmp" )
  systemProperties[ 'test.build.data' ] = new String( "${buildDir}/test/data" )
}

platformTest {
  runPlatformTests = false

  delete( "target" ) // hardcoded path in hadoop2

  forkEvery = 1 // static fields on the platform test get munged otherwise

  systemProperties[ 'hadoop.log.dir' ] = new String( "${buildDir}/test/log" )
  systemProperties[ 'hadoop.tmp.dir' ] = new String( "${buildDir}/test/tmp" )
  systemProperties[ 'test.build.data' ] = new String( "${buildDir}/test/data" )
}

javadoc {

  classpath = files( project.sourceSets.test.compileClasspath )

  configure( options ) {
    links << "http://hadoop.apache.org/docs/r${hadoop2Version}/api/".toString()
  }
}