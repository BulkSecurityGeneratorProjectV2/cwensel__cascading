/*
 * Copyright (c) 2007-2022 The Cascading Authors. All Rights Reserved.
 *
 * Project and contact information: https://cascading.wensel.net/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

task stopPlatforms( type: Exec ) {
  def killFile = new File( buildDir, 'stopPlatforms.sh' )
  doFirst {
    killFile.getParentFile().mkdirs()
    killFile.write( """\
XARGS=xargs
type gxargs >/dev/null 2>&1 && XARGS=gxargs 
echo found: 
echo `jps | grep -e MRAppMaster -e DAGApp -e TezChild -e YarnChild`
jps | grep -e MRAppMaster -e DAGApp -e TezChild -e YarnChild | cut -f1 -d" " | \$XARGS -r kill -9
"""
    )
    println "killing hung daemons"
  }

  standardOutput = new ByteArrayOutputStream();
  commandLine 'bash', killFile

  doLast{
    println "stop results:"
    println standardOutput.toString()
  }
}

task platformTest( type: Test, dependsOn: stopPlatforms ) {
  ext.runPlatformTests = true
  useJUnit()
  maxHeapSize '1000m'

  filter {
    includeTestsMatching "*PlatformTest"
  }

  systemProperties = setTestingProperties()

  doFirst() {
    if(runPlatformTests){
    rootProject.ext.testRoots << systemProperties[ 'test.output.root' ]

    // so that the include above finds the tests and runs them
    setTestClassesDirs( files( project.sourceSets.test.output.classesDirs, project( ':cascading-platform' ).sourceSets.test.output.classesDirs ) )
    }
  }
}

platformTest.shouldRunAfter test

tasks.getByPath( ':cascading-platform:platformCompareTest' ).dependsOn << platformTest

check {
  dependsOn << platformTest
}